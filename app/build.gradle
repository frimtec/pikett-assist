apply plugin: 'com.android.application'
apply plugin: "de.mannodermaus.android-junit5"
apply plugin: 'jacoco-android'

// Third party library versions
def secure_sms_proxy_api_version = '1.3.5'
def android_billingclient_version = '2.1.0'
def shadowfax_proswipebutton_version = '1.2.2'
def threetenabp_version = '1.2.2'
def takisoft_preferencex_version = '1.1.0'
def junit_version = '5.6.0'

jacoco {
    toolVersion = "0.8.4"
}

System.setProperty("line.separator", "\n")
Properties versionProps = new Properties()
def versionPropsFile = file('version.properties')
if (versionPropsFile.exists()) {
    versionProps.load(new FileInputStream(versionPropsFile))
}
int vcode = (versionProps['VERSION_CODE'] ?: "0").toInteger() + 1
String vname = (versionProps['VERSION_NAME'] ?: "0.0.1")
versionProps['VERSION_CODE'] = vcode.toString()
versionProps['VERSION_NAME'] = vname
versionProps.store(versionPropsFile.newWriter(), null)

def purchaseValidationKeyProperties = new Properties()
try {
    purchaseValidationKeyProperties.load(new FileInputStream(rootProject.file("purchaseValidationKey.properties")))
} catch (Exception e) {
    purchaseValidationKeyProperties.put("purchase.validation.key", "\"NOT_SET\"")
}

coveralls {
    jacocoReportPath 'app/build/reports/jacoco/jacocoTestReleaseUnitTestReport/jacocoTestReleaseUnitTestReport.xml'
    sourceDirs += ['app/src/main/java']
}

jacocoAndroidUnitTestReport {
    csv.enabled false
    html.enabled false
    xml.enabled true
}

tasks.withType(Test) {
    jacoco.includeNoLocationClasses = true
}

android {
    compileSdkVersion 29
    defaultConfig {
        applicationId "com.github.frimtec.android.pikettassist"
        minSdkVersion 24
        targetSdkVersion 29
        versionCode vcode
        versionName vname
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        buildConfigField("String", "PURCHASE_VALIDATION_KEY", purchaseValidationKeyProperties['purchase.validation.key'])
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation fileTree(include: ['*.jar', '*.aar'], dir: 'lib')
    implementation 'androidx.preference:preference:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'com.google.android.material:material:1.0.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation "in.shadowfax:proswipebutton:$shadowfax_proswipebutton_version"
    implementation "com.jakewharton.threetenabp:threetenabp:$threetenabp_version"
    implementation "com.android.billingclient:billing:$android_billingclient_version"
    implementation "com.github.frimtec:secure-sms-proxy-api:$secure_sms_proxy_api_version"
    implementation "com.takisoft.preferencex:preferencex:$takisoft_preferencex_version"
    implementation "com.takisoft.preferencex:preferencex-ringtone:$takisoft_preferencex_version"
    implementation "com.takisoft.preferencex:preferencex-datetimepicker:$takisoft_preferencex_version"
    testImplementation "org.junit.jupiter:junit-jupiter-api:$junit_version"
    testImplementation "org.junit.jupiter:junit-jupiter-params:$junit_version"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junit_version"
    testImplementation "org.assertj:assertj-core:3.11.1"
    testImplementation "org.mockito:mockito-core:3.1.0"
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
}
