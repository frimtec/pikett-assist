apply plugin: 'com.android.application'
apply plugin: "de.mannodermaus.android-junit5"
apply plugin: 'jacoco'

// Third party library versions
def secure_sms_proxy_api_version = '3.4.2'
def android_billingclient_version = '7.1.1'
def slidetoact_version = '0.11.0'
def takisoft_preferencex_version = '1.1.0'
def junit_version = '5.11.3'
def work_version = "2.9.1"
def retrofit_version = "2.11.0"

System.setProperty("line.separator", "\n")
Properties versionProps = new Properties()
def versionPropsFile = file('version.properties')
if (versionPropsFile.exists()) {
    versionProps.load(new FileInputStream(versionPropsFile))
}

int vcode = Integer.parseInt((versionProps['VERSION_CODE'] == null || versionProps['VERSION_CODE'].startsWith('$')) ? String.valueOf((int) ((System.currentTimeMillis() / 1000 / 60) - 26_797_800)) : versionProps['VERSION_CODE'])
String vname = (versionProps['VERSION_NAME'] == null || versionProps['VERSION_NAME'].startsWith('$')) ? '0.0.0' : versionProps['VERSION_NAME']

def purchaseValidationKeyProperties = new Properties()
try {
    purchaseValidationKeyProperties.load(new FileInputStream(rootProject.file("purchaseValidationKey.properties")))
} catch (Exception ignored) {
    purchaseValidationKeyProperties.put("purchase.validation.key", "\"" + System.getProperty("purchase.validation.key", "NOT_SET") + "\"")
}

jacoco {
    toolVersion = "0.8.7"
}

android {
    compileSdk 35
    defaultConfig {
        applicationId "com.github.frimtec.android.pikettassist"
        minSdkVersion 26
        targetSdkVersion 35
        versionCode vcode
        versionName vname
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        buildConfigField("String", "PURCHASE_VALIDATION_KEY", purchaseValidationKeyProperties['purchase.validation.key'])
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    testOptions {
        unitTests.returnDefaultValues = true
    }
    namespace 'com.github.frimtec.android.pikettassist'

    flavorDimensions = ["version"]
    productFlavors {
        playstore {
        }
        oss {
            versionNameSuffix "-oss"
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar', '*.aar'], dir: 'lib')
    implementation "androidx.work:work-runtime:$work_version"
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'com.google.android.material:material:1.12.0'
    implementation 'com.vdurmont:semver4j:3.1.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'com.google.code.gson:gson:2.11.0'
    implementation "com.ncorti:slidetoact:$slidetoact_version"
    implementation "com.takisoft.preferencex:preferencex:$takisoft_preferencex_version"
    implementation "com.takisoft.preferencex:preferencex-ringtone:$takisoft_preferencex_version"
    implementation "com.takisoft.preferencex:preferencex-datetimepicker:$takisoft_preferencex_version"
    playstoreImplementation "com.android.billingclient:billing:$android_billingclient_version"
    implementation "com.github.frimtec:secure-sms-proxy-api:$secure_sms_proxy_api_version@aar"
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"
    testImplementation "org.junit.jupiter:junit-jupiter-api:$junit_version"
    testImplementation "org.junit.jupiter:junit-jupiter-params:$junit_version"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junit_version"
    testImplementation "org.assertj:assertj-core:3.26.3"
    testImplementation "org.mockito:mockito-core:5.14.2"
    testImplementation 'com.github.tomakehurst:wiremock:3.0.1'
    androidTestImplementation 'androidx.test:runner:1.6.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.6.1'

}
