apply plugin: 'com.android.application'
apply plugin: "de.mannodermaus.android-junit5"
apply plugin: 'jacoco'

System.setProperty("line.separator", "\n")
Properties versionProps = new Properties()
def versionPropsFile = file('version.properties')
if (versionPropsFile.exists()) {
    versionProps.load(new FileInputStream(versionPropsFile))
}

int vcode = Integer.parseInt((versionProps['VERSION_CODE'] == null || versionProps['VERSION_CODE'].startsWith('$')) ? String.valueOf((int) ((System.currentTimeMillis() / 1000 / 60) - 26_797_800)) : versionProps['VERSION_CODE'])
String vname = (versionProps['VERSION_NAME'] == null || versionProps['VERSION_NAME'].startsWith('$')) ? '0.0.0' : versionProps['VERSION_NAME']

def purchaseValidationKeyProperties = new Properties()
try {
    purchaseValidationKeyProperties.load(new FileInputStream(rootProject.file("purchaseValidationKey.properties")))
} catch (Exception ignored) {
    purchaseValidationKeyProperties.put("purchase.validation.key", "\"" + System.getProperty("purchase.validation.key", "NOT_SET") + "\"")
}

jacoco {
    toolVersion = "0.8.12"
}

tasks.register("unitTestCoverageReport", JacocoReport) {
    group = "Coverage"
    dependsOn tasks.testOssDebugUnitTest
    reports {
        html.required = true
        xml.required = true
    }
    executionData.from = layout.buildDirectory.dir("outputs/unit_test_code_coverage/ossDebugUnitTest/testOssDebugUnitTest.exec")
    classDirectories.from = layout.buildDirectory.dir("intermediates/javac/ossDebug/compileOssDebugJavaWithJavac/classes")
    sourceDirectories.from = layout.projectDirectory.dir("src/main/java")
}

android {
    compileSdkVersion(36)
    defaultConfig {
        applicationId "com.github.frimtec.android.pikettassist"
        minSdkVersion 26
        targetSdkVersion 36
        versionCode vcode
        versionName vname
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        buildConfigField("String", "PURCHASE_VALIDATION_KEY", purchaseValidationKeyProperties['purchase.validation.key'])
    }
    buildTypes {
        debug {
            enableUnitTestCoverage true
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    testOptions {
        unitTests.returnDefaultValues = true
    }
    namespace 'com.github.frimtec.android.pikettassist'

    flavorDimensions = ["version"]
    productFlavors {
        playstore {
        }
        oss {
            versionNameSuffix "-oss"
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar', '*.aar'], dir: 'lib')
    implementation libs.androidx.work.runtime
    implementation(libs.androidx.constraintlayout)
    implementation(libs.androidx.material)
    implementation(libs.semver4j)
    implementation(libs.androidx.legacy.support.v4)
    implementation(libs.gson)
    implementation(libs.slidetoact)
    implementation(libs.preferencex)
    implementation(libs.preferencex.ringtone)
    implementation(libs.preferencex.datetimepicker)
    playstoreImplementation(libs.billing)
    implementation(libs.secure.sms.proxy.api)
    implementation(libs.retrofit)
    implementation(libs.retrofit.converter.gson)
    testImplementation(libs.junit.jupiter.api)
    testImplementation(libs.junit.jupiter.params)
    testRuntimeOnly(libs.junit.jupiter.engine)
    testImplementation(libs.assertj.core)
    testImplementation(libs.mockito.core)
    testImplementation(libs.wiremock)
    androidTestImplementation(libs.androidx.test.runner)
    androidTestImplementation(libs.androidx.espresso.core)
}
